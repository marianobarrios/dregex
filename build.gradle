plugins {
    id 'java'
    id 'jvm-test-suite'
    id 'signing'
    id 'maven-publish'
    id "com.diffplug.spotless" version "7.2.1"
    id "com.github.spotbugs" version "6.4.2"
}

compileJava {
    options.release = 11
    options.compilerArgs.add('-Xlint:all')
}

compileTestJava {
    options.release = 11
    options.compilerArgs.add('-Xlint:all')
}

spotbugs {
    ignoreFailures = false
    showProgress = true
    reportsDir = file("$buildDir/spotbugs")
    excludeFilter = file('spotbugs-exclude.xml')
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/spotbugs.html")
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/spotbugsTest.html")
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // regex parser
    implementation 'org.jparsec:jparsec:3.1'

    // log
    implementation 'org.slf4j:slf4j-api:2.0.17'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.5.18'

    // test framework
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4'
}

var testJavaToolchain = System.getenv('TEST_JAVA_TOOLCHAIN')

testing {
    suites {
        test {
            useJUnitJupiter()
            targets {
                all {
                    testTask.configure {
                        if (testJavaToolchain != null) {
                            javaLauncher.set(
                                    javaToolchains.launcherFor {
                                        languageVersion.set(JavaLanguageVersion.of(testJavaToolchain))
                                    }
                            )
                        }
                    }
                }
            }
        }
    }
}


javadoc {
    exclude "dregex/impl"
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        dregex(MavenPublication) {
            groupId = 'com.github.marianobarrios'
            artifactId = 'dregex'
            version = '0.9.0-SNAPSHOT'
            from components.java
            pom {
                name = 'dregex'
                 description = 'Dregex is a Java library that implements a regular expression engine using deterministic ' +
                 'finite automata (DFA). It supports some Perl-style features and yet retains linear matching time, and ' +
                 'also offers set operations.'
                url = 'https://github.com/marianobarrios/dregex'
                licenses {
                    license {
                        name = 'BSD 2-Clause "Simplified" License'
                        url = 'https://opensource.org/license/bsd-2-clause/'
                    }
                }
                developers {
                    developer {
                        name = 'Mariano Barrios'
                        email = 'marbar@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:marianobarrios/dregex.git'
                    url = 'scm:git@github.com:marianobarrios/dregex.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.findProperty('sonatypeUsername')
                password = project.findProperty('sonatypePassword')
            }
        }
    }
}

signing {
    sign publishing.publications.dregex
}
